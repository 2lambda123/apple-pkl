// File gets rendered to .circleci/config.yml via git hook. 
amends "PklCI.pkl"

import "jobs/BuildNativeJob.pkl"
import "jobs/GradleCheckJob.pkl"
import "jobs/DeployJob.pkl"
import "jobs/SimpleGradleJob.pkl"

prb {
  jobs = buildAndTestJobs
}

main {
  jobs {
    ...buildAndTestJobs
    new {
      ["deploy-snapshot"] {
        requires = buildAndTestJobs
        context {
          "pkl-maven-release"
        }
      }
    }
  }
}

release {
  jobs {
    ...releaseJobs
    // do GitHub release first because we can overwrite the tag.
    // publishing to Maven Central is final.
    new {
      ["github-release"] {
        requires = releaseJobs
        context = "pkl-github-release"
      }
    }
    new {
      ["deploy-release"] {
        requires { "github-release" }
        context {
          "pkl-maven-release"
        }
      }
    }
  }
}

local buildNativeJobs: Mapping<String, BuildNativeJob> = new {
  for (_dist in List("release", "snapshot")) {
    for (_arch in List("amd64", "aarch64")) {
      ["pkl-cli-linux-\(_arch)-\(_dist)"] {
        arch = _arch 
        os = "linux"
        isRelease = _dist == "release"
      }
    }
    ["pkl-cli-macos-amd64-\(_dist)"] {
      arch = "amd64"
      os = "macOS"
      isRelease = _dist == "release"
    }
    ["pkl-cli-linux-alpine-amd64-\(_dist)"] {
      arch = "amd64"
      os = "linux"
      musl = true
      isRelease = _dist == "release"
    }
  }
}

local gradleCheckJobs: Mapping<String, GradleCheckJob> = new {
//   ["gradle-check-jdk11"] {
//     javaVersion = "11.0"
//     isRelease = false
//   }
//   ["gradle-check-jdk17"] {
//     javaVersion = "17.0"
//     isRelease = false
//   }
}

jobs {
  for (jobName, job in buildNativeJobs) {
    [jobName] = job.job
  }
  for (jobName, job in gradleCheckJobs) {
    [jobName] = job.job
  }
  ["bench"] = new SimpleGradleJob { command = "bench:jmh" }.job
  ["gradle-compatibility"] = new SimpleGradleJob {
    name = "gradle compatibility"
    command = #"""
      :pkl-gradle:build \
        :pkl-gradle:compatibilityTestReleases \
        :pkl-gradle:compatibilityTestCandidate
      """#
  }.job
  ["deploy-snapshot"] = new DeployJob { command = "publishToSonatype" }.job
  ["deploy-release"] {
    docker {
      new {
        image = "cimg/openjdk:17.0"
      }
    }
    steps {
      new RunStep {
        command = """
          echo "Fake deploy! Replace me."
          """
      }
    }
  }
  // TODO enable this and delete the dummy one
  // ["deploy-release"] = new DeployJob { command = "closeAndReleaseSonatypeStagingRepository" }.job
  ["github-release"] {
    docker {
      new {
        image = "maniator/gh:v2.40.1"
      }
    }
    steps {
      new AttachWorkspaceStep { at = "." }
      new RunStep {
        name = "Publish release on GitHub"
        command = #"""
          gh release create "${CIRCLE_TAG}" \
            --title "${CIRCLE_TAG}" \
            --target "${CIRCLE_SHA1}" \
            --verify-tag \
            --notes "Release notes: https://pkl-lang.org/main/current/release-notes/changelog.html#release-${CIRCLE_TAG}" \
            --repo "${CIRCLE_PROJECT_USERNAME}/${CIRCLE_PROJECT_REPONAME}" \
            pkl-cli/build/executable/*
          """#
      }
    }
  }
}

local baseJobs: Listing<String> = new {
  ...gradleCheckJobs.keys
  "bench"
}

local buildAndTestJobs = (baseJobs) {
  ...buildNativeJobs.keys.filter((it) -> it.endsWith("snapshot"))
}

local releaseJobs = (baseJobs) {
  ...buildNativeJobs.keys.filter((it) -> it.endsWith("release"))
}
